# =============================================================================
# LiveKit: voice-room SFU / RTC service
# =============================================================================

locals {
  livekit_host = "${local.livekit_subdomain}.${var.DOMAIN}"
  livekit_url  = "wss://${local.livekit_host}"

  livekit_config = yamlencode({
    port = 7880

    rtc = {
      tcp_port        = local.livekit_rtc_tcp_port
      udp_port        = local.livekit_rtc_udp_port
      use_external_ip = local.livekit_rtc_use_external_ip
    }

    turn = {
      enabled   = local.livekit_turn_enabled
      domain    = local.livekit_host
      udp_port  = local.livekit_turn_udp_port
      tls_port  = local.livekit_turn_tls_port
      cert_file = "/etc/livekit-tls/tls.crt"
      key_file  = "/etc/livekit-tls/tls.key"
    }

    keys = {
      (local.livekit_api_key) = random_password.livekit_api_secret.result
    }

    room = {
      auto_create       = true
      empty_timeout     = 300
      departure_timeout = 20
      enabled_codecs = [
        {
          mime = "audio/opus"
        }
      ]
    }

    logging = {
      level = local.livekit_log_level
    }
  })
}

resource "random_password" "livekit_api_secret" {
  length  = 48
  special = false
}

resource "kubernetes_secret_v1" "livekit" {
  metadata {
    name = "livekit"
  }

  data = {
    api_key      = local.livekit_api_key
    api_secret   = random_password.livekit_api_secret.result
    "config.yml" = local.livekit_config
  }
}

resource "kubernetes_deployment_v1" "livekit" {
  metadata {
    name = "livekit"
  }

  spec {
    replicas = 1

    strategy {
      type = "Recreate"
    }

    selector {
      match_labels = {
        app = "livekit"
      }
    }

    template {
      metadata {
        labels = {
          app = "livekit"
        }
        annotations = {
          config_hash = sha256(local.livekit_config)
        }
      }

      spec {
        host_network                     = true
        dns_policy                       = "ClusterFirstWithHostNet"
        termination_grace_period_seconds = 30

        container {
          image = local.livekit_image
          name  = "livekit"

          command = ["/livekit-server"]
          args    = ["--config", "/etc/livekit/config.yml"]

          port {
            container_port = 7880
            name           = "http"
            protocol       = "TCP"
          }

          port {
            container_port = local.livekit_rtc_tcp_port
            host_port      = local.livekit_rtc_tcp_port
            name           = "rtc-tcp"
            protocol       = "TCP"
          }

          port {
            container_port = local.livekit_rtc_udp_port
            host_port      = local.livekit_rtc_udp_port
            name           = "rtc-udp"
            protocol       = "UDP"
          }

          port {
            container_port = local.livekit_turn_udp_port
            host_port      = local.livekit_turn_udp_port
            name           = "turn-udp"
            protocol       = "UDP"
          }

          port {
            container_port = local.livekit_turn_tls_port
            host_port      = local.livekit_turn_tls_port
            name           = "turn-tls"
            protocol       = "TCP"
          }

          resources {
            limits = {
              cpu    = "1000m"
              memory = "1Gi"
            }
            requests = {
              cpu    = "250m"
              memory = "256Mi"
            }
          }

          readiness_probe {
            tcp_socket {
              port = "http"
            }
            initial_delay_seconds = 5
            period_seconds        = 10
            failure_threshold     = 6
          }

          liveness_probe {
            tcp_socket {
              port = "http"
            }
            initial_delay_seconds = 15
            period_seconds        = 20
            failure_threshold     = 5
          }

          volume_mount {
            name       = "config"
            mount_path = "/etc/livekit"
            read_only  = true
          }

          volume_mount {
            name       = "tls"
            mount_path = "/etc/livekit-tls"
            read_only  = true
          }
        }

        volume {
          name = "config"

          secret {
            secret_name = kubernetes_secret_v1.livekit.metadata[0].name
          }
        }

        volume {
          name = "tls"

          secret {
            secret_name = "livekit-tls"
          }
        }
      }
    }
  }
}

resource "kubernetes_service_v1" "livekit" {
  metadata {
    name = "livekit-sv"
  }

  spec {
    selector = {
      app = "livekit"
    }

    port {
      name        = "http"
      port        = 80
      target_port = "http"
    }
  }
}

resource "kubernetes_ingress_v1" "livekit" {
  metadata {
    name = "livekit-ingress"
    annotations = {
      "kubernetes.io/ingress.class"                    = "nginx"
      "cert-manager.io/cluster-issuer"                 = "letsencrypt-prod"
      "acme.cert-manager.io/http01-edit-in-place"      = "true"
      "nginx.ingress.kubernetes.io/proxy-read-timeout" = "3600"
      "nginx.ingress.kubernetes.io/proxy-send-timeout" = "3600"
      "nginx.ingress.kubernetes.io/proxy-http-version" = "1.1"
    }
  }

  spec {
    tls {
      hosts       = [local.livekit_host]
      secret_name = "livekit-tls"
    }

    rule {
      host = local.livekit_host
      http {
        path {
          path      = "/"
          path_type = "Prefix"
          backend {
            service {
              name = kubernetes_service_v1.livekit.metadata[0].name
              port {
                name = "http"
              }
            }
          }
        }
      }
    }
  }
}
